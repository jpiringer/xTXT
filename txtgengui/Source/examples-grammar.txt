TITLE: tutorial 1
CODE:
# tutorial 1
START = begin; # a START symbol is always needed
# don't forget the semicolon ; at the end of each line
$$

TITLE: tutorial 2
CODE:
# tutorial 2
START = SECOND; # a START symbol is always needed
SECOND = all words written only in capital letters (and numbers) are substitued by their definition;
$$

TITLE: tutorial 3
CODE:
# tutorial 3
START = EITHER | OR; # symbols separated by | are chosen randomly, try it a couple of times
EITHER = this;
OR = that;
$$

TITLE: tutorial 4
CODE:
# tutorial 4
START = EITHER OR; # symbols separated by a space are joined
EITHER = this;
OR = that;
$$

TITLE: tutorial 5
CODE:
# tutorial 5
START = FIRST+SECOND; # symbols separated by + are joined without a space
FIRST = ver;
SECOND = bindung;
$$

TITLE: tutorial 6
CODE:
# tutorial 6
START = FIRST+"SECOND"; # characters in quotes " are not seen as symbols
FIRST = ver;
SECOND = bindung;
$$

TITLE: tutorial 7
CODE:
# tutorial 7
START = first line\nsecond line; # to separate lines use the sequence \n (btw: n stands for newline)
$$

TITLE: tutorial 8
CODE:
# tutorial 8
START = A A A A A A;
A = but | though| and | too| B;
B = i | you | he | she | START; # you can use symbols in symbol definitions
$$

TITLE: tutorial 9
CODE:
# tutorial 9
START = infinite START; # but don't do this!!!
$$

TITLE: tutorial 10
CODE:
# tutorial 10
START = A A A A A A A A A;
A {permutate} = a | b | c | d; # with permutate all options are exhausted before they can appear again
$$

TITLE: tutorial 11
CODE:
# tutorial 11
START = A A A A A A A A A;
A {order} = a | b | c | d; # with order all options are selected in the order they appear
$$

TITLE: tutorial 12
CODE:
# tutorial 12: conditions
START = {X = 1} C; # change the value behind "X = " to 2, 3, ...
C {X == 1} = eins;
C {X == 2} = zwei;
C {X >= 2} = viel;
$$

TITLE: tutorial 13
CODE:
# tutorial 13: counter
START = {COUNTER = 0} C;
C {COUNTER <= 99} = {COUNTER++} COUNTER C;
C {COUNTER > 99} = ende;
$$

TITLE: tutorial 14
CODE:
# tutorial 14: gerade ungerade
START = {COUNTER = 0} C;
C {COUNTER <= 99} = {COUNTER++} COUNTER C;
C {COUNTER > 99} = ende;
$$

TITLE: tempora
CODE:
START = SATZ+.;

SATZ {permutate}    = TEMPPRAES SATZPRAES | TEMPPAST SATZPAST | TEMPFUT SATZFUT;

SATZPRAES {permutate} = VERBPRAESSING SATZCONTSING | VERBPRAESPLUR SATZCONTPLUR;
SATZPAST {permutate} = VERBPASTSING SATZCONTSING | VERBPASTPLUR SATZCONTPLUR;
SATZFUT {permutate} = VERBFUTSING1 SATZCONTSING VERBFUTSING2 | VERBFUTPLUR1 SATZCONTPLUR VERBFUTPLUR2;

VERBPRAESSING = ist;
VERBPRAESPLUR = sind;

VERBPASTSING = war;
VERBPASTPLUR = waren;

VERBFUTSING1 = wird;
VERBFUTSING2 = gewesen sein;
VERBFUTPLUR1 = werden;
VERBFUTPLUR2 = gewesen sein;

SATZCONTSING {permutate} = alles besser | nichts wie zuvor | alles vergeblich | alles einerlei | alles schlechter | uns alles einerlei;
SATZCONTPLUR {permutate} = wir glücklich | wir unerfahren | wir unvorsichtig | wir übervorsichtig | wir genau | wir entschlossen zu allem;

TEMPPRAES {permutate} = heute | momentan | jetzt | in diesem moment;
TEMPPAST {permutate} = vorgestern | gestern | vor einem jahr | vor einer woche | gerade eben | voriges monat;
TEMPFUT {permutate} = morgen | übermorgen | bald | gleich | in einer woche | in einem jahr | in der zukunft | in einem monat;
$$

TITLE: wittgenstein
CODE:
START = {COUNTER=1} PARAGRAPH PARAGRAPH PARAGRAPH PARAGRAPHS PARAGRAPHS;

PARAGRAPHS = PARAGRAPH PARAGRAPHS | PARAGRAPH;
PARAGRAPH = COUNTER+. START1 START1 SENTENCES {COUNTER++}\n\n;
SENTENCES = START1 SENTENCES | START1;

START1 = START1A+. | START1A+. | START1A+. | START1A+. | PREFIX START1A+. | PREFIXQ START1A+? | FRAGE;
START1A = SATZ1 | SATZ2 | SATZ3 | SATZ4;

PREFIX    = ich kann mich darin nicht irren: | ich könnte sagen: | wenn ich sagte: | das heisst: | die antwort kann auch sein: | es ist seltsam: | man sagt nicht: | und dennoch: | wir sagen: | ich könnte auch so sagen: | man könnte auch sagen: | es scheint: | das wichtigste ist hier dies:;
PREFIXQ    = soll ich sagen: | heißt das nun: | ist es falsch, zu sagen:;

SATZ1 = SPTRANS OBJEKT;
SATZ2 = SPINTRANS;
SATZ1O2 = SATZ1 | SATZ2;
SATZ3 = SATZ1O2+, CONJ CONDSATZ;
SATZ4 = wenn CONDSATZ+, dann CONDSATZ2;


CONDSATZ = CONDSATZTRANS | CONDSATZINTRANS;
CONDSATZTRANS = SUBJEKTSING OBJEKTSING PREDTRANSSING | SUBJEKTPLUR OBJEKTPLUR PREDTRANSPLUR;
CONDSATZINTRANS    = SUBJEKTSING PREDINTRANSSING | SUBJEKTPLUR PREDINTRANSPLUR;

CONDSATZ2 = CONDSATZTRANS2 | CONDSATZINTRANS2;
CONDSATZTRANS2 = PREDTRANSSING SUBJEKTSING OBJEKTSING | PREDTRANSPLUR SUBJEKTPLUR OBJEKTPLUR;
CONDSATZINTRANS2 = PREDINTRANSSING SUBJEKTSING | PREDINTRANSPLUR SUBJEKTPLUR;

FRAGE = FRAGEPREFIX FRAGE1A | FRAGE1A | FRAGE1A;
FRAGEPREFIX = man möchte fragen: | frage:;
FRAGE1A = FRAGEWORT PREDINTRANSSING SUBJEKTSING+? | FRAGEWORT PREDINTRANSPLUR SUBJEKTPLUR+?;
FRAGEWORT = was | wie | wo;

SPTRANS = SUBJEKTSING PREDTRANSSING | SUBJEKTPLUR PREDTRANSPLUR;
SPINTRANS = SUBJEKTSING PREDINTRANSSING | SUBJEKTPLUR PREDINTRANSPLUR;

OBJEKT = OBJEKTSING | OBJEKTPLUR;

ARTIKELSINGW = die;
ARTIKELSINGM = der;
ARTIKELSINGN = das;

SUBJEKTSING1W = wahrheit | vorstellung | antwort | frage | negation | sprachverwendung | absicht | erwartung | philosophie | frage;
SUBJEKTSING1M = satz | verstand | sinn | mensch;
SUBJEKTSING1N = wort | erkennen | sprachspiel | wissen | experiment;

GENITIVSING1M = satzes | verstandes | sinns | menschens;
GENITIVSING1N = wortes | erkennens | sprachspiels | wissens | kindes;

GENITIVSING    = der SUBJEKTSING1W | des GENITIVSING1M | des GENITIVSING1N;

ADJSINGDET = unzweifelhafte | unbeabsichtigte | zweifelhafte | überprüfte | unfehlbare | zusammenhanglose;

SUBJEKTSING1A = ARTIKELSINGN SUBJEKTSING1N | ARTIKELSINGM SUBJEKTSING1M | ARTIKELSINGW SUBJEKTSING1W;
SUBJEKTSING1ADJ    = ARTIKELSINGN ADJSINGDET SUBJEKTSING1N | ARTIKELSINGM ADJSINGDET SUBJEKTSING1M | ARTIKELSINGW ADJSINGDET SUBJEKTSING1W;
SUBJEKTSING = SUBJEKTSING1A | SUBJEKTSING1ADJ | SUBJEKTSING1A GENITIVSING | SUBJEKTSING1ADJ GENITIVSING;
PREDTRANSSING = sieht | untersucht | teilt | liest;
PREDINTRANSSING    = spricht | lernt | irrt;
OBJEKTSING = den satz | mich | den sinn | den zweifel | das spiel | das gedächtnis;

SUBJEKTPLUR1 = gedanken | farben | sätze | antworten | fragen | worte | dinge | fragen;
SUBJEKTPLUR    = SUBJEKTPLUR1 | ADJPLUR SUBJEKTPLUR1;
PREDTRANSPLUR = sprechen | lehren | lesen;
PREDINTRANSPLUR    = sprechen | spielen | beginnen | lernen;
OBJEKTPLUR = die sätze | die zweifel | die wörter;

ADJPLUR = eindeutige | einzigartige | verschiedene | unzweifelhafte | unmittelbare;

CONJ = während | wo | wenn | weshalb | worauf;
$$

TITLE: grammarrhythm
CODE:
START = S+.\n+S+.\n+S+.\n+S+.;
S = tri XI | ti XF | dha XH;
XI = ikt dha tri ikt dha ge na XD | ikt XJ | ikt XG;
XF = dha XJ | XG;
XH = ti dha tri ikt XB | tri ikt dha XC;
XD = dha ti dha ge dhee na ge na;
XJ = tri ikt XA;
XG = dha ti XA;
XB = dha tri ikt XD;
XC = ti XB | ti dha ti XD | tri ikt dha XE;
XA = dha XB;
XE = dha XD | ge XD;
$$
